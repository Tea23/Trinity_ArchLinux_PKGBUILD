# Maintainer: Pawel 'l0ner' Soltys <pwslts@gmail.com>
# Contributor: Calvin Morrison <mutantturkey@gmail.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-tdelibs
pkgver=14.0.0
pkgrel=1
pkgdesc="Trinity Desktop core libraries and applications - TDE upstream GIT"
arch=('i686' 'x86_64')
url='http://scm.trinitydesktop.org/scm/git/tdelibs'
license=('GPL')
groups=('tde-core' 'tde-base')
depends=('aspell'
	'hspell'
	'jasper'
	'libcups'
	'libidn'
	'libutempter'
	'libxcomposite'
	'libxslt'
	'libxtst'
	'openexr'
	'pcre'
	'tde-arts'
	'tde-libart-lgpl'
	'xorg-iceauth'
	'xorg-xmessage'
	'xorg-xprop'
	'xorg-xset'
	'xorg-xsetroot')
makedepends=('pkgconfig' 'cmake')
provides=('tdelibs')
conflicts=('trinity-kdelibs' 'trinity-tdelibs' 'kdemod3-kdelibs' 'kdelibs3')
replaces=('trinity-kdelibs' 'trinity-tdelibs')
options=('staticlibs' 'libtool' '!makeflags' '!strip')
source=('http://tde-mirror.yosemite.net/trinity/releases/R14.0.3/tdelibs-R14.0.3.tar.bz2')

md5sums=('52ac28865adcc8b5a0176a2228004ac2')

install='trinity-tdelibs.install'

build() {
  msg "Setting PATH, CMAKE and Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /etc/profile.d/tqt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh

  cd "${srcdir}/${pkgname//tde-}"

  ## patch /usr/bin/ld: cannot find -ldbus-1-tqt
  #msg "/usr/bin/ld: cannot find -ldbus-1-tqt..."
  #patch -Np1 -i "${srcdir}/tdelibs-tdecore-libdirs.diff"

  cd $srcdir
  msg "Creating out-of-source build directory: ${srcdir}/${_builddir}"
  mkdir -p build
  cd build

  msg "Starting cmake..."
  cmake ${srcdir}/tdelibs \
    -DCMAKE_INSTALL_PREFIX=${TDEDIR} \
    -DSYSCONF_INSTALL_DIR=${TDEDIR}/etc \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DWITH_ARTS=ON \
    -DWITH_ALSA=ON \
    -DWITH_LIBART=ON \
    -DWITH_LIBIDN=ON \
    -DWITH_SSL=ON \
    -DWITH_CUPS=ON \
    -DWITH_LUA=OFF \
    -DWITH_TIFF=ON \
    -DWITH_JASPER=ON \
    -DWITH_OPENEXR=ON \
    -DWITH_UTEMPTER=ON \
    -DWITH_AVAHI=ON \
    -DWITH_PAM=ON \
    -DWITH_PCRE=ON \
    -DWITH_GCC_VISIBILITY=ON \
    -DWITH_TDEHWLIB_DAEMONS=ON \
    -DWITH_UPOWER=ON \
    -DWITH_UDISKS2=ON \
    -DWITH_LZMA=ON \
    -DWITH_XRANDR=ON \
    -DWITH_XCOMPOSITE=ON \
    -DWITH_ASPELL=ON \
    -DWITH_HSPELL=ON
#      \
#     -DCMAKE_BUILD_TYPE=Debug

  msg "Building - $pkgname..."
  make $NUMJOBS
}

package() {
  msg "Packaging - $pkgname-$pkgver"
  cd ${srcdir}/build
  make -j1 DESTDIR="$pkgdir" install
}
