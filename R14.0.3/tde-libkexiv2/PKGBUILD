# Maintainer: Michael Manley <mmanley@nasutek.com>
# Contributor: David C. Rankin <drankinatty at gmail dot com>

pkgname=tde-libkexiv2
pkgver=14.0.3
pkgrel=1
pkgdesc="The EXIV2 Library interface for TDE kipi-plugins"
arch=('i686' 'x86_64')
url="http://scm.trinitydesktop.org/scm/git/${pkgname#*-}"
license=('GPL')
groups=('tde-base')
depends=('tde-tdebase' 'exiv2')
makedepends=('pkgconfig' 'autoconf' 'imake')
optdepends=()
provides=("${pkgname#*-}")
conflicts=("trinity-${pkgname#*-}")
replaces=("trinity-${pkgname#*-}")
options=('staticlibs' 'libtool' '!emptydirs')
# install='pkgname.install'
source=("http://tde-mirror.yosemite.net/trinity/releases/R${pkgver}/libraries/libkexiv2-R${pkgver}.tar.bz2")
md5sums=('0ed113016ea05442fb16457b5c544a64')

build() {

  cd ${srcdir}

  ## Environment config
  msg "Setting PATH, Trinity Environment variables"
  # Source the QT and TDE profile
  [ "$QTDIR" = "" ] && . /etc/profile.d/tqt3.sh       # or tqt3.sh
  [ "$TDEDIR" = "" ] && . /etc/profile.d/trinity.sh   # or tde.sh

  ## Generate config files and update with autoreconf
  cd ${srcdir}/libraries/${pkgname#*-}

  msg "Copying system libtool files...."
  cp /usr/share/aclocal/libtool.m4 ./admin/libtool.m4.in
  cp /usr/share/libtool/build-aux/ltmain.sh ./admin/ltmain.sh

  msg "Running make -f admin/Makefile.common ...."
  make -f admin/Makefile.common

  ## configure
  msg "Configuring - ${pkgname}..."

  ./configure \
    --prefix=${TDEDIR} \
    --with-qt-dir=${QTDIR} \
    --with-qt-includes=${QTDIR}/include \
    --with-qt-libraries=${QTDIR}/lib \
    --localstatedir=/var \
    --enable-debug=full \
    --enable-closure

  msg "Building - ${pkgname}..."
  make $NUMJOBS

}

package() {
  msg "Packaging - $pkgname-$pkgver"

  cd ${srcdir}/libraries/${pkgname#*-}            # use for non-out-of-source

  make -j1 DESTDIR="$pkgdir" install
}
